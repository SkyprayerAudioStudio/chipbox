apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'


def getVersionCode = { ->
    def proc = Runtime.getRuntime().exec("git rev-list --all --count")

    def result = proc.waitFor()
    if (result != 0) {
        return 1 // no git revisions
    }

    // Add a million because of previous versionCode problems.
    // Multiply by 10 so LSB can represent build type.
    return (proc.getInputStream().readLines().get(0).toInteger() * 10 ) + 1000000
}

def getVersionName = { ->
    def proc = Runtime.getRuntime().exec("git describe --tags")

    def result = proc.waitFor()
    if (result != 0) {
        return "git-error"
    }

    return proc.getInputStream().readLines().get(0)
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "net.sigmabeta.chipbox"

        minSdkVersion 21
        targetSdkVersion 25

        versionName getVersionName()

        // This block is different from the one you use to link Gradle to your CMake build script.
        externalNativeBuild {
            cmake {
                // Use the following syntax when passing arguments to variables:
                // arguments "-DVAR_NAME=VALUE"
                arguments "-DANDROID=true",
                        "-DANDROID_NATIVE_API_LEVEL=android-21",
                        "-DANDROID_STL=c++_shared"
            }
        }
    }

    // Use this block to link Gradle to your CMake build script.
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    signingConfigs {
        release {
            storeFile file("chipbox.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-debug'
            jniDebuggable true
        }
    }

    flavorDimensions "abi"

    productFlavors {
        arm {
            dimension "abi"
            versionCode getVersionCode() + 1

            ndk {
                abiFilter "armeabi-v7a"
            }
        }

        x86 {
            dimension "abi"
            versionCode getVersionCode() + 3

            ndk {
                abiFilter "x86"
            }
        }

        arm_64 {
            dimension "abi"
            versionCode getVersionCode() + 5

            ndk {
                abiFilter "arm64-v8a"
            }
        }

        x86_64 {
            dimension "abi"
            versionCode getVersionCode() + 7

            ndk {
                abiFilter "x86_64"
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    // Contains Lollipop UI View backports.
    implementation "com.android.support:appcompat-v7:$support_lib_version"

    // RecyclerView backports.
    implementation "com.android.support:recyclerview-v7:$support_lib_version"

    // Contains new Material Design views. Automatically includes support-v4
    implementation "com.android.support:design:$support_lib_version"

    // MediaStyle notifications.
    implementation "com.android.support:support-media-compat:$support_lib_version"

    // Enable RxJava-style asynrchronous calls
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.0.2'

    // Display images loaded from storage.
    implementation 'com.squareup.picasso:picasso:2.5.2'

    // Contains the Kotlin Standard Library
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Copy album art more easily.
    implementation 'commons-io:commons-io:2.4'

    // Simplified error logging.
    implementation 'com.jakewharton.timber:timber:4.5.1'

    // Crash Reporting
    implementation('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

    // Dependency Injection.
    implementation "com.google.dagger:dagger:${dagger_version}"
    kapt "com.google.dagger:dagger-compiler:${dagger_version}"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
}